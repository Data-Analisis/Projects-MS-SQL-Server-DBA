

CREATE   PROCEDURE [srv].[AutoKillLongSessionUser]
	@oldsec INT=10 --максимально допустимая длительность выполнения запроса от пользователей
AS
BEGIN
	/*
		автоудаление долгих запросов от пользовательских сессий через удаление самих этих сессий
	*/

	SET QUERY_GOVERNOR_COST_LIMIT 0;
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @session_id INT;
	DECLARE @sql NVARCHAR(2000);
	DECLARE @error_message NVARCHAR(4000);
	
	--собираем все сессии, в которых запущены запросы с SSMS и выполняющиеся более 10 сек
	SELECT ES.[session_id]
	      ,ES.[login_time]					as [session_login_time]
	      ,ES.[host_name]					as [session_host_name]
	      ,ES.[program_name]				as [session_program_name]
	      ,ES.[host_process_id]				as [session_host_process_id]
	      ,ES.[client_version]				as [session_client_version]
	      ,ES.[client_interface_name]		as [session_client_interface_name]
	      ,ES.[security_id]					as [session_security_id]
	      ,ES.[login_name]					as [session_login_name]
	      ,ES.[nt_domain]					as [session_nt_domain]
	      ,ES.[nt_user_name]				as [session_nt_user_name]
	      ,ES.[status]						AS [session_status]
	      ,ES.[cpu_time]					AS [session_cpu_time]
	      ,ES.[memory_usage]				as [session_memory_usage]
	      ,ES.[total_scheduled_time]		as [session_total_scheduled_time]
	      ,ES.[total_elapsed_time]			as [session_total_elapsed_time]
	      ,ES.[endpoint_id]					as [session_endpoint_id]
	      ,ES.[last_request_start_time]		as [session_last_request_start_time]
	      ,ES.[last_request_end_time]		as [session_last_request_end_time]
	      ,ES.[reads]						as [session_reads]
	      ,ES.[writes]						as [session_writes]
	      ,ES.[logical_reads]				as [session_logical_reads]
	      ,ES.[is_user_process]				as [session_is_user_process]
	      ,ES.[text_size]					as [session_text_size]
	      ,ES.[language]					as [session_language]
	      ,ES.[date_format]					as [session_date_format]
	      ,ES.[date_first]					as [session_date_first]
	      ,ES.[quoted_identifier]			as [session_quoted_identifier]
	      ,ES.[arithabort]					as [session_arithabort]
	      ,ES.[ansi_null_dflt_on]			as [session_ansi_null_dflt_on]
	      ,ES.[ansi_defaults]				as [session_ansi_defaults]
	      ,ES.[ansi_warnings]				as [session_ansi_warnings]
	      ,ES.[ansi_padding]				as [session_ansi_padding]
	      ,ES.[ansi_nulls]					as [session_ansi_nulls]
	      ,ES.[concat_null_yields_null]		as [session_concat_null_yields_null]
	      ,ES.[transaction_isolation_level]	as [session_transaction_isolation_level]
	      ,ES.[lock_timeout]				as [session_lock_timeout]
	      ,ES.[deadlock_priority]			as [session_deadlock_priority]
	      ,ES.[row_count]					as [session_row_count]
	      ,ES.[prev_error]					as [session_prev_error]
	      ,ES.[original_security_id]		as [session_original_security_id]
	      ,ES.[original_login_name]			as [session_original_login_name]
	      ,ES.[last_successful_logon]		as [session_last_successful_logon]
	      ,ES.[last_unsuccessful_logon]		as [session_last_unsuccessful_logon]
	      ,ES.[unsuccessful_logons]			as [session_unsuccessful_logons]
	      ,ES.[group_id]					as [session_group_id]
	      ,ES.[database_id]					as [session_database_id]
	      ,ES.[authenticating_database_id]	as [session_authenticating_database_id]
	      ,ES.[open_transaction_count]		AS [session_open_transaction_count]
		  ,ER.[request_id]					as [request_id]
	      ,ER.[start_time]					as [request_start_time]
	      ,ER.[status]						AS [request_status]
	      ,ER.[command]						as [request_command]
	      ,ER.[sql_handle]					as [request_sql_handle]
	      ,ER.[statement_start_offset]		as [request_statement_start_offset]
	      ,ER.[statement_end_offset]		as [request_statement_end_offset]
	      ,ER.[plan_handle]					as [request_plan_handle]
	      ,ER.[user_id]						as [request_user_id]
	      ,ER.[connection_id]				as [request_connection_id]
	      ,ER.[blocking_session_id]			as [request_blocking_session_id]
	      ,ER.[wait_type]					as [request_wait_type]
	      ,ER.[wait_time]					as [request_wait_time]
	      ,ER.[last_wait_type]				as [request_last_wait_type]
	      ,ER.[wait_resource]				as [request_wait_resource]
	      ,ER.[open_transaction_count]		as [request_open_transaction_count]
	      ,ER.[open_resultset_count]		as [request_open_resultset_count]
	      ,ER.[transaction_id]				as [request_transaction_id]
	      ,ER.[percent_complete]			as [request_percent_complete]
	      ,ER.[estimated_completion_time]	as [request_estimated_completion_time]
	      ,ER.[cpu_time]					AS [request_cpu_time]
	      ,ER.[total_elapsed_time]			as [request_total_elapsed_time]
	      ,ER.[scheduler_id]				as [request_scheduler_id]
	      ,ER.[task_address]				as [request_task_address]
	      ,ER.[reads]						as [request_reads]
	      ,ER.[writes]						as [request_writes]
	      ,ER.[logical_reads]				as [request_logical_reads]
	      ,ER.[text_size]					as [request_text_size]
	      ,ER.[language]					as [request_language]
	      ,ER.[date_format]					as [request_date_format]
	      ,ER.[date_first]					as [request_date_first]
	      ,ER.[quoted_identifier]			as [request_quoted_identifier]
	      ,ER.[arithabort]					as [request_arithabort]
	      ,ER.[ansi_null_dflt_on]			as [request_ansi_null_dflt_on]
	      ,ER.[ansi_defaults]				as [request_ansi_defaults]
	      ,ER.[ansi_warnings]				as [request_ansi_warnings]
	      ,ER.[ansi_padding]				as [request_ansi_padding]
	      ,ER.[ansi_nulls]					as [request_ansi_nulls]
	      ,ER.[concat_null_yields_null]		as [request_concat_null_yields_null]
	      ,ER.[transaction_isolation_level]	as [request_transaction_isolation_level]
	      ,ER.[lock_timeout]				as [request_lock_timeout]
	      ,ER.[deadlock_priority]			as [request_deadlock_priority]
	      ,ER.[row_count]					as [request_row_count]
	      ,ER.[prev_error]					as [request_prev_error]
	      ,ER.[nest_level]					as [request_nest_level]
	      ,ER.[granted_query_memory]		as [request_granted_query_memory]
	      ,ER.[executing_managed_code]		as [request_executing_managed_code]
	      ,ER.[group_id]					as [request_group_id]
	      ,ER.[query_hash]					as [request_query_hash]
	      ,ER.[query_plan_hash]				as [request_query_plan_hash]
	      ,ER.[statement_sql_handle]		as [request_statement_sql_handle]
	      ,ER.[statement_context_id]		as [request_statement_context_id]
	      --,ER.[dop]							as [request_dop]
	      --,ER.[parallel_worker_count]		as [request_parallel_worker_count]
	      --,ER.[external_script_request_id]	as [request_external_script_request_id]
		  ,DB_Name(coalesce(ER.[database_id], ES.[database_id])) as [DBName]
		  ,(select top(1) [text] from sys.dm_exec_sql_text(ER.[sql_handle])) as [TSQL]
		  ,(select top(1) [query_plan] from sys.dm_exec_query_plan(ER.[plan_handle])) as [QueryPlan]
		  , CAST(N'' AS NVARCHAR(4000))		AS [ERROR_MESSAGE]
	INTO #tbl
	FROM sys.dm_exec_sessions AS ES WITH(READUNCOMMITTED)
	INNER JOIN sys.dm_exec_requests AS ER WITH(READUNCOMMITTED) ON ES.[session_id]=ER.[session_id]
	WHERE ER.[start_time]<=DATEADD(SECOND, -@oldsec, GetDate())
	  AND ES.[program_name] LIKE N'Microsoft SQL Server Management Studio%';
	
	DECLARE sql_cursor CURSOR LOCAL FOR
	select [session_id]
	from #tbl;
	
	OPEN sql_cursor;
		  
	FETCH NEXT FROM sql_cursor   
	INTO @session_id;
	
	while (@@FETCH_STATUS = 0)
	BEGIN
		BEGIN TRY
			SET @sql=N'KILL '+CAST(@session_id AS NVARCHAR(255));
			EXEC sys.sp_executesql @sql;--KILL @session_id;
		END TRY
		BEGIN CATCH
			UPDATE t
			SET [error_message]=ERROR_MESSAGE()
			FROM #tbl AS t
			WHERE [session_id]=@session_id;
		END CATCH
		
		FETCH NEXT FROM sql_cursor
		INTO @session_id;
	end
	
	CLOSE sql_cursor;
	DEALLOCATE sql_cursor;
	
	INSERT INTO srv.KillSessionArchive(
										[session_id]
										,[session_login_time]
										,[session_host_name]
										,[session_program_name]
										,[session_host_process_id]
										,[session_client_version]
										,[session_client_interface_name]
										,[session_security_id]
										,[session_login_name]
										,[session_nt_domain]
										,[session_nt_user_name]
										,[session_status]
										,[session_cpu_time]
										,[session_memory_usage]
										,[session_total_scheduled_time]
										,[session_total_elapsed_time]
										,[session_endpoint_id]
										,[session_last_request_start_time]
										,[session_last_request_end_time]
										,[session_reads]
										,[session_writes]
										,[session_logical_reads]
										,[session_is_user_process]
										,[session_text_size]
										,[session_language]
										,[session_date_format]
										,[session_date_first]
										,[session_quoted_identifier]
										,[session_arithabort]
										,[session_ansi_null_dflt_on]
										,[session_ansi_defaults]
										,[session_ansi_warnings]
										,[session_ansi_padding]
										,[session_ansi_nulls]
										,[session_concat_null_yields_null]
										,[session_transaction_isolation_level]
										,[session_lock_timeout]
										,[session_deadlock_priority]
										,[session_row_count]
										,[session_prev_error]
										,[session_original_security_id]
										,[session_original_login_name]
										,[session_last_successful_logon]
										,[session_last_unsuccessful_logon]
										,[session_unsuccessful_logons]
										,[session_group_id]
										,[session_database_id]
										,[session_authenticating_database_id]
										,[session_open_transaction_count]
										,[request_id]
										,[request_start_time]
										,[request_status]
										,[request_command]
										,[request_sql_handle]
										,[request_statement_start_offset]
										,[request_statement_end_offset]
										,[request_plan_handle]
										,[request_user_id]
										,[request_connection_id]
										,[request_blocking_session_id]
										,[request_wait_type]
										,[request_wait_time]
										,[request_last_wait_type]
										,[request_wait_resource]
										,[request_open_transaction_count]
										,[request_open_resultset_count]
										,[request_transaction_id]
										,[request_percent_complete]
										,[request_estimated_completion_time]
										,[request_cpu_time]
										,[request_total_elapsed_time]
										,[request_scheduler_id]
										,[request_task_address]
										,[request_reads]
										,[request_writes]
										,[request_logical_reads]
										,[request_text_size]
										,[request_language]
										,[request_date_format]
										,[request_date_first]
										,[request_quoted_identifier]
										,[request_arithabort]
										,[request_ansi_null_dflt_on]
										,[request_ansi_defaults]
										,[request_ansi_warnings]
										,[request_ansi_padding]
										,[request_ansi_nulls]
										,[request_concat_null_yields_null]
										,[request_transaction_isolation_level]
										,[request_lock_timeout]
										,[request_deadlock_priority]
										,[request_row_count]
										,[request_prev_error]
										,[request_nest_level]
										,[request_granted_query_memory]
										,[request_executing_managed_code]
										,[request_group_id]
										,[request_query_hash]
										,[request_query_plan_hash]
										,[request_statement_sql_handle]
										,[request_statement_context_id]
										--,[request_dop]
										--,[request_parallel_worker_count]
										--,[request_external_script_request_id]
										,[DBName]
										,[TSQL]
										,[QueryPlan]
										,[ERROR_MESSAGE]							
									  )
								 SELECT [session_id]
										,[session_login_time]
										,[session_host_name]
										,[session_program_name]
										,[session_host_process_id]
										,[session_client_version]
										,[session_client_interface_name]
										,[session_security_id]
										,[session_login_name]
										,[session_nt_domain]
										,[session_nt_user_name]
										,[session_status]
										,[session_cpu_time]
										,[session_memory_usage]
										,[session_total_scheduled_time]
										,[session_total_elapsed_time]
										,[session_endpoint_id]
										,[session_last_request_start_time]
										,[session_last_request_end_time]
										,[session_reads]
										,[session_writes]
										,[session_logical_reads]
										,[session_is_user_process]
										,[session_text_size]
										,[session_language]
										,[session_date_format]
										,[session_date_first]
										,[session_quoted_identifier]
										,[session_arithabort]
										,[session_ansi_null_dflt_on]
										,[session_ansi_defaults]
										,[session_ansi_warnings]
										,[session_ansi_padding]
										,[session_ansi_nulls]
										,[session_concat_null_yields_null]
										,[session_transaction_isolation_level]
										,[session_lock_timeout]
										,[session_deadlock_priority]
										,[session_row_count]
										,[session_prev_error]
										,[session_original_security_id]
										,[session_original_login_name]
										,[session_last_successful_logon]
										,[session_last_unsuccessful_logon]
										,[session_unsuccessful_logons]
										,[session_group_id]
										,[session_database_id]
										,[session_authenticating_database_id]
										,[session_open_transaction_count]
										,[request_id]
										,[request_start_time]
										,[request_status]
										,[request_command]
										,[request_sql_handle]
										,[request_statement_start_offset]
										,[request_statement_end_offset]
										,[request_plan_handle]
										,[request_user_id]
										,[request_connection_id]
										,[request_blocking_session_id]
										,[request_wait_type]
										,[request_wait_time]
										,[request_last_wait_type]
										,[request_wait_resource]
										,[request_open_transaction_count]
										,[request_open_resultset_count]
										,[request_transaction_id]
										,[request_percent_complete]
										,[request_estimated_completion_time]
										,[request_cpu_time]
										,[request_total_elapsed_time]
										,[request_scheduler_id]
										,[request_task_address]
										,[request_reads]
										,[request_writes]
										,[request_logical_reads]
										,[request_text_size]
										,[request_language]
										,[request_date_format]
										,[request_date_first]
										,[request_quoted_identifier]
										,[request_arithabort]
										,[request_ansi_null_dflt_on]
										,[request_ansi_defaults]
										,[request_ansi_warnings]
										,[request_ansi_padding]
										,[request_ansi_nulls]
										,[request_concat_null_yields_null]
										,[request_transaction_isolation_level]
										,[request_lock_timeout]
										,[request_deadlock_priority]
										,[request_row_count]
										,[request_prev_error]
										,[request_nest_level]
										,[request_granted_query_memory]
										,[request_executing_managed_code]
										,[request_group_id]
										,[request_query_hash]
										,[request_query_plan_hash]
										,[request_statement_sql_handle]
										,[request_statement_context_id]
										--,[request_dop]
										--,[request_parallel_worker_count]
										--,[request_external_script_request_id]
										,[DBName]
										,[TSQL]
										,[QueryPlan]
										,[ERROR_MESSAGE]
	FROM #tbl;
	
	DROP TABLE #tbl;
END

GO
EXECUTE sp_addextendedproperty @name = N'MS_Description', @value = N'Автоудаление долгих запросов от пользовательских сессий через удаление самих этих сессий', @level0type = N'SCHEMA', @level0name = N'srv', @level1type = N'PROCEDURE', @level1name = N'AutoKillLongSessionUser';

